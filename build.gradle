buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:0.9.0"
  }
}

plugins {
  id 'groovy'
  id 'idea'
  id 'maven'
  id 'com.jfrog.artifactory' version '3.1.1'
//  id 'org.ajoberstar.grgit' version '1.3.2'
}

apply plugin: 'com.gradle.plugin-publish'
//apply plugin: 'org.ajoberstar.grgit'

def versionNumber = '0.1-SNAPSHOT'

pluginBundle {
  website = 'http://www.github.com/tlberglund'
  vcsUrl = 'http://www.github.com/tlberglund/asciidoctor-presentations'

  description = 'A plugin for building DataStax Asciidoctor curriculum'

  plugins {
    presentationPlugin {
      id = 'com.timberglund.presentation.plugin'
      displayName = 'Asciidoctor curriculum plugin'
      tags = ['presentation', 'asciidoc', 'asciidoctor', 'deckjs']
      version = versionNumber
    }
  }
}

version = versionNumber
group = 'com.timberglund'
def artifact = 'presentation-plugin'


repositories {
  jcenter()
}

dependencies {
  compile 'org.codehaus.groovy:groovy-all:2.3.10'
  compile gradleApi()
  compile 'com.github.houbie:lesscss-gradle-plugin:1.0.3-less-1.7.0'
  compile 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.2'
  runtime 'org.asciidoctor:asciidoctorj-groovy-dsl:1.0.0.preview2'
  runtime 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.8'
  compile 'org.ajoberstar:grgit:1.3.0'
}


jar {
  baseName = artifact
}


task wrapper(type: Wrapper) {
  gradleVersion = '2.4'
}


task sourceJar(type: Jar) {
  description = 'An archive of the source code for Maven Central'
  baseName = artifact
  classifier = 'sources'
  from sourceSets.main.groovy
}


task groovydocJar(type: Jar, dependsOn: groovydoc) {
  description = 'An archive of the GroovyDocs for Maven Central'
  baseName = artifact
  classifier = 'javadoc'
  from fileTree(groovydoc.destinationDir)
}
